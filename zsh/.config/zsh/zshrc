# if not running interactively, don't do anything
[[ $- != *i* ]] && return

# convenience variables, for internal use
zshconfigdir="$HOME/.config/zsh"
zshdatadir="$HOME/.local/share/zsh"

# configure prompt
PROMPT='%B[%c %(?..%F{black})%#%F{fg}]%f%b '
RPROMPT='%(?..%F{black}%B%?%b%F{fg})'

# configure history
HISTFILE="$zshdatadir/histfile"
HISTSIZE=6000
SAVEHIST=5000
HISTORY_IGNORE='(q(uit)#|l[sal]( #| ##[^|]#|)|lkj|(g|git) (st|lo|di|dis|loa|lop|show)( #| ##[^|]#)|man ##*)'
setopt histignoredups histignorespace histfindnodups
setopt appendhistory sharehistory histexpiredupsfirst

# configure user interface
setopt nobeep ignore_eof

# configure completions
fpath=("$zshdatadir/completions" $fpath)
setopt nocomplete_aliases completeinword
zstyle ':completion:*' menu select

# configure jobs
setopt notify longlistjobs

# configure help module
autoload -Uz run-help
unalias run-help
alias help=run-help

# source zle configuration
source "$zshconfigdir/zshzle"

# source zsh aliases, functions, and function definitions
source "$zshconfigdir/zshaliases"
fpath=("$zshconfigdir/functions" $fpath)
for file in "$zshconfigdir/functions/"*; do
  autoload -Uz $(basename "$file")
done

# source all drop-in config files.
# all files should use the convenience variables provided
# above ($zshconfigdir, etc) when required.
for file in "$zshconfigdir/zshrc.d/"*; do
  source "$file"
done

# *lastly*, enable and configure zsh completion module
autoload -Uz compinit
compinit

# unset convenience variables
unset zshconfigdir
unset zshdatadir
