# ============
# key bindings
# ============

bindkey -v
KEYTIMEOUT=1

# vim keybindings
bindkey -M viins '^?' backward-delete-char
bindkey -M viins '^[[3~' delete-char
bindkey -M viins '^M' accept-line

# bash keybindings
bindkey -M main '^L' clear-screen
bindkey -M viins '^L' clear-screen
bindkey -M vicmd '^L' clear-screen

# misc keybindings

# =================================
# custom widgets and behaviors
# =================================

# don't eat trailing spaces after autocompleting
ZLE_REMOVE_SUFFIX_CHARS=''

# change cursor style depending on mode
zle-keymap-select () {
  case $KEYMAP in
    vicmd)      echo -ne '\033[2 q';; # block cursor
    viins|main) echo -ne '\033[6 q';; # line cursor
  esac
}
zle-line-init () {
  echo -ne '\033[6 q' # line cursor
}
accept-line () {
  echo -ne '\033[2 q' # block cursor
  zle .accept-line
}

# clear scrollback buffer as well
clear-screen () {
  echo -ne '\033c'
  zle .clear-screen
}

# load previously-defined custom widgets
zle -N zle-keymap-select
zle -N zle-line-init
zle -N accept-line
zle -N clear-screen
